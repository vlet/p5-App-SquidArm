#!/usr/bin/perl
use strict;
use warnings;
use Getopt::Long;
use AnyEvent;
use AnyEvent::Log;
use AnyEvent::Debug;
use Proc::Daemon;
use App::SquidArm::Daemon;
use App::SquidArm::Conf;

my $config   = '/etc/squid3/sarm.conf';
my $debug    = '';
my $nodaemon = '';
my $cwd      = undef;

GetOptions(
    "config=s"  => \$config,
    "nodaemon"  => \$nodaemon,
    "workdir=s" => \$cwd,
    "debug"     => \$debug
) or die "Error in command line arguments\n";

my $conf = App::SquidArm::Conf->new( config => $config )->parse;

# Init daemon process
Proc::Daemon::Init( { defined $cwd ? ( 'work_dir' => $cwd ) : () } )
  unless $nodaemon;

my $log_level =
    $debug                          ? 'debug'
  : defined $conf->tag('log_level') ? $conf->tag('log_level')
  :                                   'warn';

$AnyEvent::Log::FILTER->level($log_level);
$AnyEvent::Log::LOG->log_to_file( $conf->tag('log_file') )
  if defined $conf->tag('log_file');

our $SHELL = AnyEvent::Debug::shell "unix/", $conf->tag('debug_unixsocket')
  if $conf->tag('debug_unixsocket');

my ( $w, $term, $daemon, @sig );

$term = sub {
    $w->send();
    $daemon->stop if $daemon;
};

$daemon = App::SquidArm::Daemon->new(
    on_error => $term,
    %{ $conf->tags }
)->run();

$w = AE::cv;

push @sig, AE::signal TERM => $term;
push @sig, AE::signal INT  => $term;

push @sig, AE::signal HUP => sub {
    my $newconf =
      eval { App::SquidArm::Conf->new( config => $config )->parse; };
    if ($@) {
        AE::log error => "error while parsing config file. Using old config";
    }
    else {
        $conf = $newconf;
    }
    eval { $daemon->hup( $conf->tags ); };
    $term->() if $@;
};

$w->recv();
